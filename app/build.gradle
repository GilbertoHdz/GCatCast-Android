apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


def PROJECT_keyAlias
def PROJECT_keyPassword
def PROJECT_storeFile
def PROJECT_storePassword

if (rootProject.isBitriseBuild) {
    PROJECT_keyAlias = System.getenv("KEY_ALIAS")
    PROJECT_keyPassword = System.getenv("KEY_PASSWORD")
    PROJECT_storeFile = System.getenv("STORE_FILE")
    PROJECT_storePassword = System.getenv("STORE_PASSWORD")

} else {
    // Create a variable called keystorePropertiesFile, and initialize it to your
    // keystore.properties file, in the rootProject folder.
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    // Initialize a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()
    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    PROJECT_keyAlias = keystoreProperties['keyAlias']
    PROJECT_keyPassword = keystoreProperties['keyPassword']
    PROJECT_storeFile = file(keystoreProperties['storeFile'])
    PROJECT_storePassword = keystoreProperties['storePassword']
}

android {
    signingConfigs {
        release {
            keyAlias PROJECT_keyAlias
            keyPassword PROJECT_keyPassword
            storeFile file(PROJECT_storeFile)
            storePassword PROJECT_storePassword
        }
    }
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 33
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.manitosdev.gcatcast"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        free {
            versionNameSuffix = 'free'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0'
    implementation "com.github.bumptech.glide:glide:4.11.0"

    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation 'com.squareup.retrofit2:converter-simplexml:2.6.1'
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.1"

    // Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    // AdMob
    freeImplementation('com.google.firebase:firebase-ads:18.0.0')

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    def exo_player_version = "2.11.3"
    implementation "com.google.android.exoplayer:exoplayer:$exo_player_version"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
